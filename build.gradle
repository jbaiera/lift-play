// Minimal build.gradle for Lift project and Eclipse integration
apply {
    plugin 'scala'
    plugin 'war'
    plugin 'jetty'
    plugin 'eclipse'
}

ext {
  scalaVersion = '2.10'
  scalaMinor = '2'
  liftVersion = '2.5'
  specs2Version = "1.14"
}

jettyRun.contextPath = "/"

configurations {
    scalaCompiler
    scalaLibrary
}

repositories {
  mavenCentral()
}

eclipse {
  classpath.containers += ["org.scala-ide.sdt.launching.SCALA_CONTAINER"]
  classpath.minusConfigurations = [configurations.scalaLibrary]
  classpath.plusConfigurations =  ([configurations.scalaCompiler]  + classpath.plusConfigurations)
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.useCompileDaemon = false
  scalaCompileOptions.useAnt = false
  scalaCompileOptions.additionalParameters = ['-target:jvm-1.6']
}

dependencies {
    scalaCompiler "org.scala-lang:scala-compiler:$scalaVersion.$scalaMinor"
    scalaLibrary "org.scala-lang:scala-library:$scalaVersion.$scalaMinor"

    compile "org.scala-lang:scala-library:$scalaVersion.$scalaMinor", 
    	"net.liftweb:lift-mapper_$scalaVersion:$liftVersion", 
    	"net.liftweb:lift-webkit_$scalaVersion:$liftVersion",
    	"net.liftweb:lift-util_$scalaVersion:$liftVersion",
    	"net.liftweb:lift-actor_$scalaVersion:$liftVersion",
    	"net.liftweb:lift-common_$scalaVersion:$liftVersion",
    	"net.liftweb:lift-json_$scalaVersion:$liftVersion", 
        "ch.qos.logback:logback-classic:1.0.13",
        "com.h2database:h2:1.3.155"

    testCompile "junit:junit:4.5", 
        "org.specs2:specs2_$scalaVersion:$specs2Version",
        'org.mortbay.jetty:jetty:6.1.25',
        'org.mortbay.jetty:jetty-util:6.1.25',
        'org.mortbay.jetty:jetty-management:6.1.25'

    providedCompile 'javax.servlet:servlet-api:2.5' 
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
